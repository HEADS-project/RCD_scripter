/* RCD scriptgrammar.jj version 2 using task joblist*/
options {
  MULTI=true;
  NODE_DEFAULT_VOID=true;
  NODE_PACKAGE="org.thingml.rcd_scripter3.parser";
  STATIC = false ;
}

PARSER_BEGIN (RcdScript3Parser)
   package org.thingml.rcd_scripter3.parser;
   import java.io.FileReader;
   public class RcdScript3Parser {
        private String fileName;
        public ASTRcdStart makeAst()throws ParseException, TokenMgrError
        {
            token_source.defaultLexState = IN_RCD_SKIP_REGION; 
            token_source.curLexState = IN_RCD_SKIP_REGION; 
                return(Start()); 
        }

        public void setFileName(String name) {
            fileName = name;
        }

   }
PARSER_END (RcdScript3Parser)

SKIP: /* WHITE SPACE */ { "\n" | "\r" | "\r\n" | "\\" | "\t" | " " | "//" | "*/" | "/*"}

/* COMMENTS */

MORE :
{
  "RCD_SCRIPT_STOP" : IN_RCD_SKIP_REGION
}

SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "##" (~["\n", "\r"])* ("\n" | "\r" | "\r\n")?>
}


<IN_RCD_SKIP_REGION>
SPECIAL_TOKEN :
{
  <RCD_SKIP_REGION: "RCD_SCRIPT_START" > : DEFAULT
}

<IN_RCD_SKIP_REGION>
MORE :
{
  < ~[] >
}



TOKEN [IGNORE_CASE]: /* Commands */
{
  <INCLUDE :("INCLUDE")>
|
  <FOR :("FOR")>
|
  <PROC :("PROC")>
|
  <RETURN :("RETURN")>
|
  <EXIT :("EXIT")>
|
  <IF:("IF")>
|
  <ELSE:("ELSE")>
|
  <TRUE:("TRUE")>
|
  <FALSE:("FALSE")>
|
  <RCD_SCRIPT_END: ("RCD_SCRIPT_END")>
|
  <AND:("AND")>
|
  <OR:("OR")>

}


TOKEN :  /* Literals */
{
  < VAR_LITERAL: <DOLLAR><ID_LITERAL> >
|
  < ID_LITERAL: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < DEC_LITERAL: (["+","-"])?(<DIGIT>)+ >
|
  < FLOAT_LITERAL: (<DIGIT>)+ <PERIOD> (<DIGIT>)+ (<DECIMAL_EXPONENT>)? >
|
  < #DECIMAL_EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
|
  < HEX_LITERAL: "0" ["x","X"] (<HEX_DIGIT>)+ >
|
  < STRING_LITERAL:
      "\""
      (   (~["\"","\\"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
  >
}

TOKEN :  /* Character groups */
{
  < LETTER: [ "a"-"z", "A"-"Z", "_" ] >
|
  < DIGIT: [ "0"-"9"] >
|
  < HEX_DIGIT: [ "0"-"9", "a"-"f", "A"-"F"] >
}

TOKEN:  /* Separators */
{
    <OBRA:("(")>
|
    <CBRA:(")")>
|
    <OCBRA:("{")>
|
    <CCBRA:("}")>
|
    <OABRA:("[")>
|
    <CABRA:("]")>
|
    <COMMA:(",")>
|
    <PERIOD:(".")>
|
    <SEMI:(";")>
|
    <COLON:(":")>
|
    <ASSIGN:("=")>
|
    <EQUAL:("==")>
|
    <GT:(">")>
|
    <LT:("<")>
|
    <GTE:(">=")>
|
    <LTE:("<=")>
|
    <NOTEQUAL:("!=")>
|
    <DOLLAR:("$")>
|
    <PLUS:("+")>
|
    <MINUS:("-")>
|
    <MUL:("*")>
|
    <DIV:("/")>
}


ASTRcdStart Start() #RcdStart : {}
{
	Script()
    <RCD_SCRIPT_END>
    {return jjtThis;}
}

void Script() : {}
{
    (
		Include()
		|
        DeclProc()
        |
        Block()
        |
        Statement()
    )*
}

void Include() :
{
    Token t;
	String fileName;
    RcdScript3Parser subParser;
    ASTRcdStart subAst;
}
{
    <INCLUDE>
    <OBRA>
    t = <STRING_LITERAL>
    <CBRA>
    <SEMI>
    {
        fileName = t.image.substring(0, t.image.length()-1).substring(1); // Remove quotes
		System.out.println("Starting to include " + fileName);
        try{
			subParser = new RcdScript3Parser(new FileReader(fileName)); 
			subAst = subParser.makeAst(); 
            subAst.dump("Include: ");
			
			if (subAst.children != null) {
				for (int i = 0; i < subAst.children.length; ++i) {
					jjtree.openNodeScope(subAst.children[i]);
					jjtree.closeNodeScope(subAst.children[i], true);
				}
			}
		}
        catch (Exception ex) 
        {
            ex.printStackTrace();
        }
		System.out.println("End of include " + fileName);
	}
}

// ************* DeclProc ***************
void DeclProc() #RcdDeclProc :
{
    Token t;
}
{
    (
        <PROC>
        t = <ID_LITERAL>
        <OBRA>
            FormParamNode()
        <CBRA>
        <OCBRA>
        ScriptNode("ProcScript")
        <CCBRA>
    )
    {
	    jjtThis.setName(t.image);
        jjtThis.setToken(token);
	}
}

void FormParamNode() #RcdNode :
{
}
{
    { jjtThis.setName("FormParam"); }
    (
        FormParam()
        (
            <COMMA>
            FormParam()
        )*
    )?
}

void FormParam() #RcdFormParam:
{
    Token t;
}
{
    t = <VAR_LITERAL>
    {
        jjtThis.setName(t.image);
        jjtThis.setToken(t); 
    }
}

// ************* Block ***************

void Block() :
{
}
{
	IfBlock()
	|
	ForBlock()
}

void IfBlock() #RcdIfBlock :
{
}
{
	<IF>
    { jjtThis.setToken(token); }
	<OBRA>
	ExprNode()
	<CBRA>
    <OCBRA>
	ScriptNode("TrueScript")
    <CCBRA>
	(
		<ELSE>
        <OCBRA>
		ScriptNode("FalseScript")
        <CCBRA>
	)?
}

void ForBlock() #RcdForBlock :
{
}
{
	<FOR>
    { jjtThis.setToken(token); }
	<OBRA>
	Var()
	<COLON>
    ExprNode()
	<CBRA>
	<OCBRA>
	ScriptNode("ForScript")
    <CCBRA>
}

void ScriptNode(String nodeName) #RcdNode :
{
}
{
    { jjtThis.setName(nodeName); }
	Script()
}

// ************* Statement ***************

void Statement() :
{
}
{
	(
		CallProc(false)
        |
        VarXXX()
        |
        ReturnStmt()
        |
        ExitStmt()
	)
	<SEMI>
}

void CallProc(boolean returnValue) #RcdCallProc :
{
}
{
	Id()
    { 
        jjtThis.setToken(token); 
        jjtThis.setReturnValue(returnValue);
    }
	<OBRA>
        ActParamNode()
	<CBRA>
}

void VarXXX() :
{
}
{
    Var()
    (
	    (
            Index()
    	)*
	    (
            Assign()
            |
            CallMethod(false)
    	)
    ) 
}

void Index() #RcdIndex:
{
    Token t;
}
{
}
	t = <OABRA>
        ExprNode()
	<CABRA>
    {
        jjtThis.setName(t.image);
        jjtThis.setToken(t); 
    }
}

void Assign() #RcdAssign:
{
    Token t;
}
{
}
	t = <ASSIGN>
    {
        jjtThis.setName(t.image);
        jjtThis.setToken(t); 
    }
	ExprNode()
}



void CallMethod(boolean returnValue) #RcdCallMethod :
{
}
{
}
    <PERIOD>
    { 
        jjtThis.setToken(token); 
        jjtThis.setReturnValue(returnValue);
    }
    Id()
	<OBRA>
        ActParamNode()
	<CBRA>
}

void ActParamNode() #RcdNode :
{
}
{
    { jjtThis.setName("ActParam"); }
    (
        ExprNode()
        (
            <COMMA>
            ExprNode()
        )*
    )?
}

void ReturnStmt() #RcdReturnStmt :
{
}
{
	<RETURN>
    { jjtThis.setToken(token); }
	<OBRA>
    (
        ExprNode()
    )?
}

void ExitStmt() #RcdExitStmt :
{
}
{
	<EXIT>
    { jjtThis.setToken(token); }
	<OBRA>
    (
        ExprNode()
    )?
}

// ************* Comparison grammar ***************

void CmpExprNode() #RcdNode :
{
}
{
    { jjtThis.setName("CmpExprNode"); }
	CmpExpr()
}

void CmpExpr() : 
{
}
{
    Expr() 
	( 
		CmpExprFollow() 
	)?
}

void CmpExprFollow() #RcdOpExpr : 
{
    Token t;
}
{
	( 
		t = <EQUAL>
		|
		t = <GT>
		|
		t = <LT>
		|
		t = <GTE>
		|
		t = <LTE>
		|
		t = <NOTEQUAL>
	) 
	Expr() 
	{
        jjtThis.setName(t.image);
        jjtThis.setToken(t); 
    }
}

// ************* Expression grammar ***************

void ExprNode() #RcdNode :
{
}
{
    { jjtThis.setName("ExprNode"); }
	Expr()
}

void Expr() : 
{
}
{
    TermExpr() 
	( 
		ExprFollow() 
	)*
}

void ExprFollow() #RcdOpExpr: 
{
    Token t;
}
{
    ( 
        t = <PLUS>
        |
        t = <MINUS>
    ) 
    TermExpr() 
    {
        jjtThis.setName(t.image);
        jjtThis.setToken(t); 
    }
}

void TermExpr() : 
{
}
{
    Factor() 
	( 
        TermExprFollow()
	)*
}

void TermExprFollow() #RcdOpExpr: 
{
    Token t;
}
{
    ( 
        t = <MUL>
        |
        t = <DIV>
    ) 
    Factor() 
    {
        jjtThis.setName(t.image);
        jjtThis.setToken(t); 
    }
}

void Factor() : {}
{
  <OBRA> Expr() <CBRA> 
  |
  LOOKAHEAD(Id() <OBRA>) 
  CallProc(true)
  |
  LOOKAHEAD(Var() <PERIOD>) 
  CallVarMethod(true)
  |
  Var() 
  | 
  Id() 
  | 
  String()
  |
  IntNum()
  |
  Bool()
//  |
//  FloatNum()
  |
  Hash()
}

void Var() #RcdVar:
{
    Token t;
}
{
    t = <VAR_LITERAL>
    {
        jjtThis.setName(t.image);
        jjtThis.setToken(t); 
    }
}

void Id() #RcdId :
{
    Token t;
}
{
    t = <ID_LITERAL>
    {
        jjtThis.setName(t.image);
        jjtThis.setToken(t); 
    }
}

void String() #RcdString :
{
    Token t;
}
{
    t = <STRING_LITERAL>
    {
        jjtThis.setName(t.image);
        jjtThis.setToken(t); 
    }
}

void IntNum() #RcdIntNum :
{
    Token t;
}
{
    (
        t = <DEC_LITERAL>
		|
        t = <HEX_LITERAL>
	)
    {
        jjtThis.setName(t.image);
        jjtThis.setToken(t); 
    }
}

void Bool() #RcdBool :
{
    Token t;
}
{
    (
        t = <TRUE>
        |
        t = <FALSE>
	)
    {
        jjtThis.setName(t.image);
        jjtThis.setToken(t); 
    }
}

void FloatNum() #RcdFloatNum :
{
    Token t;
}
{
    t = <FLOAT_LITERAL>
    {
        jjtThis.setName(t.image);
        jjtThis.setToken(t); 
    }
}

// ************* HASH grammar ***************

void Hash() #RcdHash :
{
}
{
  (
    <OCBRA> 
    {
        jjtThis.setToken(token); 
    }
	Tupl()
	( 
	    <COMMA>
		Tupl()
	)*
    <CCBRA> 
  ) 
}

void Tupl() : 
{
}
{
    (
        Expr()
        (
            <COLON>
                {
                    jjtThis.setToken(token); 
                }
            Expr() 
        )?
    ) #RcdTupl(>1)
}

